apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: "maven-publish"
apply plugin: 'kotlinx-serialization'

group = 'com.github.doordeck'

android {
    // Fallback values for NFC tiles
    def fallbackNfcUriHost = "doordeck.link"
    def fallbackNfcUriScheme = "https"

    defaultConfig {
        minSdkVersion 26
        compileSdk 34
        buildToolsVersion = '34.0.0'
        multiDexEnabled = true
        versionName "1.2"
        vectorDrawables.useSupportLibrary = true

        resValue "string", "nfc_uri_host", project.properties.get("nfcUri").getOrDefault("host", fallbackNfcUriHost)
        resValue "string", "nfc_uri_scheme", project.properties.get("nfcUri").getOrDefault("scheme", fallbackNfcUriScheme)
    }

    namespace "com.github.doordeck.ui"

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    buildTypes.each { type ->
        type.manifestPlaceholders["nfc_uri_host"] = project.properties.get("nfcUri").getOrDefault("host", fallbackNfcUriHost)
        type.manifestPlaceholders["nfc_uri_scheme"] = project.properties.get("nfcUri").getOrDefault("scheme", fallbackNfcUriScheme)
    }

    buildTypes {
        debug {
            buildConfigField "String", "VERSION_NAME", '"1.2"'
            buildConfigField "String", "BASE_URL_API", '"https://api.dev.doordeck.com"'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            buildConfigField "String", "VERSION_NAME", '"1.2"'
            buildConfigField "String", "BASE_URL_API", '"https://api.doordeck.com"'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlin {
        jvmToolchain(17)
    }

    testOptions {
        unitTests {
            // Note: without this, all Robolectric tests using BuildConfig will fail.
            includeAndroidResources = true
        }
    }
}

buildscript {

    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }

    dependencies {
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

dependencies {
    implementation "org.immutables:value-annotations:2.7.5" // or implementation, or compileOnly
    annotationProcessor "org.immutables:value:2.7.5"
    testAnnotationProcessor "org.immutables:value:2.7.5"

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation "androidx.constraintlayout:constraintlayout:2.2.1"
    implementation "androidx.recyclerview:recyclerview:1.4.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.8.7"

    implementation "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${versions.jackson}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"
    //rx
//    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // scan
    implementation('com.journeyapps:zxing-android-embedded:4.1.0') { transitive = false }

    implementation 'com.google.zxing:core:3.4.0'

    // location
    implementation 'com.google.android.gms:play-services-auth:21.3.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'

    implementation "com.doordeck.headless.sdk:doordeck-sdk:${versions.doordeck_headless_sdk}"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.8.0"


}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
            }
        }
        repositories {
            mavenLocal()
        }
    }
}
